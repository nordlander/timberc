<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<html> <head>
<link href="simplestyle.css" rel="stylesheet" type="text/css">
<title>Primes</title>
</head>

<body>
<h3>Primes</h3>
<p>
As a reactive program, this is quite degenerate: when started, it
expects
a positive integer <tt>n</tt>&gt;2 as command line argument. It
computes all primes smaller than or equal to <tt>n</tt>, prints
the number of such primes to <tt>stdout</tt> and terminates.
<p>
The algorithm used illustrates imperative programming using updatable
arrays in Timber. Here is the program:
<hr>
<pre>
module Primes where

import POSIX 

root env = class
   limit :: Int
   limit = parse (env.argv!1)
   primesBound = limit `div` log3 limit

   primes := uniarray primesBound 0
   count  := 0

   isPrime k = loop 0
      where loop n = do 
              p = primes!n
              if p*p > k then
                 result True
              elsif k `mod` p  == 0 then
                 result False
              else loop (n+1)

   checkFrom k = do
     p <- isPrime k
     if p then 
        primes!count := k
        count := count + 1
     if k < limit then checkFrom (k+1)

   result action
     primes!0 := 2
     count := 1
     checkFrom 3
     env.stdout.write (show count++"\n")
     env.exit 0


log3 :: Int -> Int
log3 n  
  | n < 3       = 0
  | otherwise   = 1 + log3 (n `div` 3)

</pre>
<hr>
Prime numbers are stored in array <tt>primes</tt>, which is
initialised with all zeros (primitive function <tt>uniarray</tt>
creates an array, whose size is given by the first argument, where
all elements are initialised to the value of the second argument
The program uses the moderately clever bound that the number
of primes up to <tt>n</tt> is at most <tt>n `div` log3 n</tt>.
<p>
The root action notes that 2 is a prime and initiates <tt>count</tt>
to 1; it then checks numbers for primeness, starting with 3 in
procedure <tt>checkFrom</tt>. The iterative check is expressed using
recursion, checking for successive numbers up to <tt>limit</tt>.
<p>
The procedure <tt>isPrime</tt> works by trial division, using already
discovered primes: to check whether <tt>k</tt> is a prime, one tries
to find a proper prime factor <tt>p</tt> such that <tt>p*p <= k</tt>. If
none is found, <tt>k</tt> is prime. 


</body> </html>
