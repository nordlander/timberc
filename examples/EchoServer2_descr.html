<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<html> <head>
<link href="simplestyle.css" rel="stylesheet" type="text/css">
<title>EchoServer2</title>
</head>

<body>
<h3>EchoServer2</h3>
<p>
This is a minor extension of <tt>EchoServer</tt>: when started, it
expects an integer command line argument <tt>maxClients</tt>, denoting
the maximal number of concurrently served clients. If more clients
connect, they are just informed that the server is busy and
disconnected.
<hr>
<pre>
module EchoServer2 where

import POSIX
import Counter

port = Port 12345

root env = class
 
    clients = new counter

    log str = action
       env.stdout.write ('[':str ++ "]\n")

    result action
       maxClients = parse (env.argv ! 1)
       env.inet.tcp.listen port (server maxClients clients log)

server maxClients clients log sock = class

   n := 1

   p = show sock.remoteHost

   echo str = action
      sock.outFile.write (show n ++"> "++str)
      n := n+1

   close = request
      clients.decr
      log (p ++ " closing")
      result ()

   neterror str = log ("Neterror: "++str)

   established = action
      cl <- clients.value
      if cl < maxClients then
         clients.incr
         log ("Connected from " ++ p)
         sock.inFile.installR echo
      else
         sock.outFile.write "Server busy"
         log ("Refused " ++ p)
         sock.close

   result Connection{..}
</pre>
<hr>
The differences to <tt>EchoServer</tt> are the following:
<p>
  We need to keep track of the number of served clients.
  For this we use a <a href="Counter_descr.html"><tt>Counter</tt></a>
  object, created by the root object.
  <p> When a new client connects, the counter value is retrieved and
  only if this value is small enough is an <tt>echo</tt> action installed;
  otherwise the client is informed that the server is busy and the
  socket closed.  When a client closes the connection, the counter is decreased.
  <p>
  A <tt>Counter</tt> object is appropriate here, since several server objects,
  each interacting with one client, interacts with it. On the
  contrary, the line number local to each server object is maintained
  in a simple integer state variable.
 
</body> </html>
